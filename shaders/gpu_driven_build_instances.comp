#version 430

struct BatchBlock
{
	int instanceCount;
	int baseInstance;
	int meshNodeMatrixId;
	int pad1;
};

struct ModelBlock
{
    uint nodeOffset;    // start index in MeshNodeBuffer
    uint nodeCount;     // how many nodes per model
	int  pad0;
	int  pad1;
};

struct GameObjectBlock
{
	mat4 model;        // 64 bytes
	int  model_index;     // 4 bytes
	int  enabled;
	int  pad1;
	int  pad2;
};

struct InstancesBlock
{
	uint gameObjectId;
	uint meshNodeMatrixId;
	uint pad0;
	uint pad1;
};

layout(std430, binding = 3) buffer BatchBuffer
{
	BatchBlock batches[];
};

layout(std430, binding = 4) buffer ModelBuffer
{
    ModelBlock models[];
};

layout(std430, binding = 5) buffer GameObjectMatrices
{
    GameObjectBlock gameObjects[];
};

layout( binding = 6 ) buffer GpuCounter
{
    uint num_gameObjects;
};

layout( binding = 7 ) buffer MeshInstanceCounterBufer
{
    uint meshInstanceCounter[];
};

layout(binding = 9) buffer InstancesBuffer {
    InstancesBlock instances[];
};

//layout( binding = 10 ) buffer BatchCounter
//{
//    uint num_batches;
//};

layout(binding = 12) buffer MeshNodeToBatchBuffer
{
    int meshNodeToBatch[]; // size = MAX_MESH_NODES
};

layout(local_size_x = 64) in;

void main()
{	
	uint gid = gl_GlobalInvocationID.x;

	if (gid >= num_gameObjects) return;

	GameObjectBlock obj = gameObjects[gid];
	ModelBlock model = models[obj.model_index];

	if (obj.enabled == 0) return;

	for (uint n = 0; n < model.nodeCount; n++) {
		uint meshNodeMatrixId = model.nodeOffset + n;

		// retrieve the actual index in the compacted batch buffer
		int batchIndex = meshNodeToBatch[meshNodeMatrixId];

		// -1 means its unused/no instances for this model
		if (batchIndex < 0) continue; 

		// get current instance count for this mesh/node then increment by one
		uint local = atomicAdd(meshInstanceCounter[meshNodeMatrixId], 1);

		// compute destination index in the instance buffer
		uint dst   = uint(batches[batchIndex].baseInstance) + local;

		// bind gameObject and store index of mesh/node for later lookup
		instances[ dst ].gameObjectId = gid;
		instances[ dst ].meshNodeMatrixId = meshNodeMatrixId;
	}
}